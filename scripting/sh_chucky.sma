// CHUCKY! - From the Child's Play movie series. "Hi. I'm Chucky. Wanna play?"

/* CVARS copy and paste to shconfig.cfg

//Chucky
chucky_level 10
chucky_cooldown 600      //Amount of time before next available respawn
chucky_knifemult 2.50    //Amount mutiplied to damage when knifing
chucky_knifespeed 720    //User speed when knife is out

*/

/*
* v1.4 - vittu - 6/27/06
*      - Updated to amxmodx only, requires amxx 1.70 or higher.
*      - Minor code clean up.
*      - Fixed possible issue with first connect, no respawn possiblity.
*      - vittu - 12/27/06
*      - Added check when respawning if pb_autokill is set to 1.
*      - Added optional model define for those who don't want the model.
*
* v1.3 - vittu - 6/14/05
*      - Minor code clean up.
*      - Cleaned up Respawn blocks and fixed possible bug with
*          no respawn on first connect.
*
* v1.2 - vittu - 3/8/05
*      - fixed small bug I introduced, with team change checking.
*      - optimized some of the code.
*
* v1.1 - vittu - 2/18/05
*      - removed useless code.
*      - removed useless model, found p_ model but wasn't worth adding.
*      - added self contained cooldown to avoid being reset by other
*         respawning heroes for next version of sh mod.
*      - added extra sound.
*      - added lots and lots and lots of checks against exploits.
*
*   Hero Created by LiToViEtBoI
*   Weapon model by TriggerJunky
*/
//---------- User Changeable Defines --------//
// Note: If you change anything from default setting you must recompile the plugin

// 0 = don't use custom KNIFE model, 1 = use custom KNIFE model [Default 1]
#define USE_MODEL 1

//------- Do not edit below this point ------//
#include <superheromod>

// GLOBAL VARIABLES
new gHeroID
new HeroName[] = "Chucky"
new bool:HasChucky[SH_MAXSLOTS+1]
new bool:ChuckyPowerUsed[SH_MAXSLOTS+1]
new CsTeams:UserTeam[SH_MAXSLOTS+1]
new bool:BombPlanted
new CvarCooldown, CvarKnifeMult
//----------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Chucky", "1.4", "LiToViEtBoI/vittu")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel 	= register_cvar("chucky_level", "1")
	new pcvarSpeed 	= register_cvar("chucky_knifespeed", "730")
	CvarKnifeMult 	= register_cvar("chucky_knifemult", "2.5")
	CvarCooldown 	= register_cvar("chucky_cooldown", "600")
	
	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(HeroName, pcvarLevel)
	sh_set_hero_info(gHeroID, "Poderes Vudú.", "Volvé de la muerte cada ronda. Obtén una Faka Sangrienta que hace más daño y te hace más rápido.")
	
	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)
	// EVENTS
	#if USE_MODEL
		register_event("CurWeapon", "weapon_change", "be", "1=1")
	#endif
	register_event("Damage", "chucky_damage", "b", "2!0")

	// LOG EVENTS
	register_logevent("round_start", 2, "1=Round_Start")
	register_logevent("round_end", 2, "1=Round_End")
	register_logevent("round_end", 2, "1&Restart_Round_")

	// Let Server know about Chucky's Variables
	sh_set_hero_speed(gHeroID, pcvarSpeed, {CSW_KNIFE}, 1)
	sh_set_hero_shield(gHeroID, true)
}

public plugin_precache()
{
	#if USE_MODEL
		precache_model("models/shmod/chucky_knife.mdl")
	#endif
	precache_sound("nihilanth/nil_comes.wav")
}
//----------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode)
{
	if ( gHeroID != heroID ) return
	 
	switch(mode) {
		case SH_HERO_ADD: {
			HasChucky[id] = true
			#if USE_MODEL
				switch_model(id)
			#endif
		}
		case SH_HERO_DROP: {
			HasChucky[id] = false
		}
	}
}
public sh_client_spawn(id)
{
	if ( HasChucky[id] && is_user_alive(id) ) {
		set_task(0.1, "chucky_weapons", id)
	}
}

public chucky_weapons(id)
	if ( is_user_alive(id) ) sh_give_weapon(id, CSW_KNIFE) 
	
#if USE_MODEL
public weapon_change(id)
{
	if ( !HasChucky[id] ) return

	new wpnid = read_data(2)
	if ( wpnid == CSW_KNIFE ) switch_model(id)
}

switch_model(id)
{
	if ( !is_user_alive(id) || !HasChucky[id] ) return
	
	new clip, ammo, wpnid = get_user_weapon(id, clip, ammo)
	
	if ( wpnid == CSW_KNIFE ) set_pev(id, pev_viewmodel2, "models/shmod/chucky_knife.mdl")
}
#endif
//----------------------------------------------------------------------------------------------
public chucky_damage(id)
{
	if ( !is_user_alive(id) ) return

	new weapon, bodypart, attacker = get_user_attacker(id, weapon, bodypart)
	if ( attacker <= 0 || attacker > SH_MAXSLOTS ) return

	if ( HasChucky[attacker] && weapon == CSW_KNIFE && is_user_alive(id) ) {
		new damage = read_data(2)
		new headshot = bodypart == 1 ? 1 : 0

		// Do extra damage
		new extraDamage = floatround(damage * get_pcvar_float(CvarKnifeMult) - damage)
		if ( extraDamage > 0 )
			shExtraDamage(id, attacker, extraDamage, "knife", headshot)
	}
}
//----------------------------------------------------------------------------------------------
public sh_client_death(victim)
{
	if ( !sh_is_inround() || !is_user_connected(victim) ) return

	// What is the users team when he dies?
	UserTeam[victim] = cs_get_user_team(victim)
	
	// Look for self to raise from dead
	if ( !is_user_alive(victim) && !ChuckyPowerUsed[victim] && HasChucky[victim] ) {
		// Chucky will raise self from dead
		new parm[1]
		parm[0] = victim
		// Respawn him faster then Zues, let this power be used before Zues's
		// never set higher then 1.9 or lower then 0.5
		/* el task esta puesto en 0.x porque segun el mas chico se activa primero ese heroe.
		mangekyou = 0,5
		chucky = 0.6
		phoenix = 0.7
		shaman = 0.8
		dr.strange = 0.9
		majin buu = 1.0
		grandmaster = 1.1 // pero esta se superpone porque es la primera en usarse 
		uchiha revenge = 1.2
		torneo = 1.5 */
		set_task(0.6, "chucky_respawn", 0, parm, 1)
	}
}
//----------------------------------------------------------------------------------------------
public chucky_respawn(parm[])
{
	new id = parm[0]

	if ( !is_user_connected(id) || is_user_alive(id) ) return
	if ( ChuckyPowerUsed[id] || !sh_is_inround() ) return

	// Check prevents respawning spectators, cs_get_user_team prevents team change respawning
	if ( UserTeam[id] != cs_get_user_team(id) ) return

	// Prevent respawn if Pod-Bot kills all bots when no players are alive and bomb has not been planted
	if ( get_cvar_num("pb_autokill") && !BombPlanted ) {
		new players[SH_MAXSLOTS], pnum

		// Check for any alive non-bots 
		get_players(players, pnum, "ac")

		// if no alive non-bots do not respawn
		if ( !pnum )
			return
	}

	emit_sound(id, CHAN_STATIC, "ambience/port_suckin1.wav", 1.0, ATTN_NORM, 0, PITCH_NORM)

	sh_chat_message(id, gHeroID, "Usaste los Poderes Vudú de %s para volver a la vida!", HeroName)

	// Double spawn prevents the no HUD glitch
	// This should eventually be changed to use a better method
	spawn(id)
	spawn(id) 
	//ExecuteHamB(Ham_CS_RoundRespawn, id)
	
	// Respawned by Chucky, it's ok to set cooldown now.
	new Float:chuckyCooldown = get_pcvar_float(CvarCooldown)
	if ( chuckyCooldown > 0.0 ) {
		set_task(chuckyCooldown, "enable_chucky", id)
		ChuckyPowerUsed[id] = true
	}

	emit_sound(id, CHAN_STATIC, "nihilanth/nil_comes.wav", 1.0, ATTN_NORM, 0, PITCH_NORM)

	shGlow(id, 75, 75, 255)
	set_task(3.0, "chucky_unglow", 0, parm, 1)
	set_task(1.0, "chucky_teamcheck", 0, parm, 1)
}
//----------------------------------------------------------------------------------------------
public chucky_unglow(parm[])
{
	new id = parm[0]
	
	if ( !is_user_connected(id) ) return

	shUnglow(id)
}
//----------------------------------------------------------------------------------------------
// This function should now be useless because of using cs_get_user_team rather than get_user_team
public chucky_teamcheck(parm[])
{
	new id = parm[0]

	if ( !is_user_connected(id) ) return

	if ( UserTeam[id] != cs_get_user_team(id) ) {
		sh_chat_message(id, gHeroID, "Cambiaste de equipo y reivivste usando %s Poderes de Vudú, ahora morirás!", HeroName)
		user_kill(id, 1)

		// Stop Chucky from respawning until round ends
		remove_task(id)
		ChuckyPowerUsed[id] = true
	}
}
//----------------------------------------------------------------------------------------------
public bomb_planted(id)
	BombPlanted = true

public round_start()
	BombPlanted = false

public round_end()
{
	BombPlanted = false
	
	// Reset the cooldown on round end, to start fresh for a new round
	for ( new id = 1; id <= SH_MAXSLOTS; id++ ) {
		if ( HasChucky[id] ) {
			remove_task(id)
			ChuckyPowerUsed[id] = false
		}
	}
}

public enable_chucky(id)
	ChuckyPowerUsed[id] = false
	
//----------------------------------------------------------------------------------------------
public client_connect(id)
{
	// Yeah don't want any left over residuals
	remove_task(id)

	// Incase variables are stale reset them
	HasChucky[id] = false
	ChuckyPowerUsed[id] = false
}
//---------------------------------------------------------------------------------------------
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang11274\\ f0\\ fs16 \n\\ par }
*/
