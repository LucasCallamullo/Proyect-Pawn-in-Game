// CHUCKY! - From the Child's Play movie series. "Hi. I'm Chucky. Wanna play?"

/* CVARS copy and paste to shconfig.cfg

//Chucky
chucky_level 10
chucky_cooldown 600      //Amount of time before next available respawn
chucky_knifemult 2.50    //Amount mutiplied to damage when knifing
chucky_knifespeed 720    //User speed when knife is out

*/

/*
* v1.4 - vittu - 6/27/06
*      - Updated to amxmodx only, requires amxx 1.70 or higher.
*      - Minor code clean up.
*      - Fixed possible issue with first connect, no respawn possiblity.
*      - vittu - 12/27/06
*      - Added check when respawning if pb_autokill is set to 1.
*      - Added optional model define for those who don't want the model.
*
* v1.3 - vittu - 6/14/05
*      - Minor code clean up.
*      - Cleaned up Respawn blocks and fixed possible bug with
*          no respawn on first connect.
*
* v1.2 - vittu - 3/8/05
*      - fixed small bug I introduced, with team change checking.
*      - optimized some of the code.
*
* v1.1 - vittu - 2/18/05
*      - removed useless code.
*      - removed useless model, found p_ model but wasn't worth adding.
*      - added self contained cooldown to avoid being reset by other
*         respawning heroes for next version of sh mod.
*      - added extra sound.
*      - added lots and lots and lots of checks against exploits.
*
*   Hero Created by LiToViEtBoI
*   Weapon model by TriggerJunky
*/
//---------- User Changeable Defines --------//
// Note: If you change anything from default setting you must recompile the plugin

//------- Do not edit below this point ------//
#include <superheromod>

// GLOBAL VARIABLES
new gHeroID
new HeroName[] = "Chucky"
new bool:HasChucky[SH_MAXSLOTS+1]

new CsTeams:UserTeam[SH_MAXSLOTS+1]
new bool:BombPlanted

new gPcvarCooldown, gPcvarKnifeMult

new const v_chucky_knife[] = "models/shmod/chucky_knife.mdl"

new const gSound_1[] = "nihilanth/nil_comes.wav"
new const gSound_2[] = "ambience/port_suckin1.wav"
//----------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Chucky", "1.4", "LiToViEtBoI/vittu")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel 	= register_cvar("chucky_level", "1")
	new pcvarSpeed 	= register_cvar("chucky_knifespeed", "730")
	gPcvarKnifeMult = register_cvar("chucky_knifemult", "2.5")
	gPcvarCooldown 	= register_cvar("chucky_cooldown", "60")
	
	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(HeroName, pcvarLevel)
	sh_set_hero_info(gHeroID, "Poderes Vudú.", "Volvé de la muerte cada ronda. Obtén una Faka Sangrienta que hace más daño y te hace más rápido.")
	
	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)
	// EVENTS
	RegisterHam(Ham_Item_Deploy, "weapon_knife", "Knife_Deploy", 1)
	register_event("Damage", "chucky_damage", "b", "2!0")

	// Let Server know about Chucky's Variables
	sh_set_hero_speed(gHeroID, pcvarSpeed, {CSW_KNIFE}, 1)
	sh_set_hero_shield(gHeroID, true)
}

public plugin_precache()
{
	precache_model(v_chucky_knife)
	precache_sound(gSound_1)
	precache_sound(gSound_2)
}
//----------------------------------------------------------------------------------------------
//			HERO INIT
//----------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode)
{
	if ( gHeroID != heroID ) return
	 
	switch(mode) {
		case SH_HERO_ADD: {
			HasChucky[id] = true
			switch_model(id)
		}
		case SH_HERO_DROP: {
			HasChucky[id] = false
		}
	}
}

//----------------------------------------------------------------------------------------------
//				Change models weapons
//----------------------------------------------------------------------------------------------
public Knife_Deploy(iEnt)
{
	new id = get_pdata_cbase(iEnt, 41, 4)	// 41 y 4 son constantes van siempre
	if ( !is_user_alive(id) || !HasChucky[id] ) return HAM_IGNORED; 
	
	set_pev(id, pev_viewmodel2, v_chucky_knife) 
	return HAM_IGNORED;
}

switch_model(id)
{	
	if ( !is_user_alive(id) ) return
	new clip, ammo, wpnid = get_user_weapon(id, clip, ammo)
	if ( wpnid == CSW_KNIFE ) set_pev(id, pev_viewmodel2, v_chucky_knife)
}

//----------------------------------------------------------------------------------------------
public chucky_damage(id)
{
	if ( !is_user_alive(id) ) return

	new weapon, bodypart, attacker = get_user_attacker(id, weapon, bodypart)
	if ( attacker <= 0 || attacker > SH_MAXSLOTS ) return

	if ( HasChucky[attacker] && weapon == CSW_KNIFE && is_user_alive(id) ) {
		new damage = read_data(2)
		new headshot = bodypart == 1 ? 1 : 0

		// Do extra damage
		new extraDamage = floatround(damage * get_pcvar_float(gPcvarKnifeMult) - damage)
		if ( extraDamage > 0 )
			shExtraDamage(id, attacker, extraDamage, "knife", headshot)
	}
} 
//----------------------------------------------------------------------------------------------
public sh_client_death(victim)
{
	if ( !sh_is_inround() || !is_user_connected(victim) ) return

	// What is the users team when he dies?
	UserTeam[victim] = cs_get_user_team(victim)
	
	// Look for self to raise from dead
	if ( !is_user_alive(victim) && !gPlayerInCooldown[victim] && HasChucky[victim] ) {
		// Chucky will raise self from dead
		new parm[1]
		parm[0] = victim
		// Respawn him faster then Zues, let this power be used before Zues's
		// never set higher then 1.9 or lower then 0.5
		/* el task esta puesto en 0.x porque segun el mas chico se activa primero ese heroe.
		mangekyou = 0,5
		chucky = 0.6
		phoenix = 0.7
		shaman = 0.8
		dr.strange = 0.9
		majin buu = 1.0
		grandmaster = 1.1 // pero esta se superpone porque es la primera en usarse 
		uchiha revenge = 1.2
		torneo = 1.5 */
		set_task(0.6, "chucky_respawn", 0, parm, 1)
	}
}
//----------------------------------------------------------------------------------------------
public chucky_respawn(parm[])
{
	new id = parm[0]

	if ( is_user_alive(id) || !sh_is_inround() ) return

	// Check prevents respawning spectators, cs_get_user_team prevents team change respawning
	if ( UserTeam[id] != cs_get_user_team(id) ) return

	// Prevent respawn if Pod-Bot kills all bots when no players are alive and bomb has not been planted
	if ( get_cvar_num("pb_autokill") && !BombPlanted ) {
		new players[SH_MAXSLOTS], pnum

		// Check for any alive non-bots 
		get_players(players, pnum, "ac")

		// if no alive non-bots do not respawn
		if ( !pnum ) return
	}

	emit_sound(id, CHAN_STATIC, gSound_2, 1.0, ATTN_NORM, 0, PITCH_NORM)

	sh_chat_message(id, gHeroID, "Usaste los Poderes Vudú de %s para volver a la vida!", HeroName)

	// Double spawn prevents the no HUD glitch
	// This should eventually be changed to use a better method
	spawn(id)
	spawn(id) 
	//ExecuteHamB(Ham_CS_RoundRespawn, id)
	
	// Respawned by Chucky, and set cooldown
	new Float:seconds = get_pcvar_float(gPcvarCooldown)
	if ( seconds > 0.0 ) sh_set_cooldown(id, seconds)

	emit_sound(id, CHAN_STATIC, gSound_1, 1.0, ATTN_NORM, 0, PITCH_NORM)

	shGlow(id, 75, 75, 255)
	set_task(3.0, "chucky_unglow", 0, parm, 1)
	set_task(1.0, "chucky_teamcheck", 0, parm, 1)
}
//----------------------------------------------------------------------------------------------
public chucky_unglow(parm[])
{
	new id = parm[0]
	if ( !is_user_connected(id) ) return
	sh_set_rendering(id)
}
//----------------------------------------------------------------------------------------------
// This function should now be useless because of using cs_get_user_team rather than get_user_team
public chucky_teamcheck(parm[])
{
	new id = parm[0]

	if ( !is_user_connected(id) ) return

	if ( UserTeam[id] != cs_get_user_team(id) ) {
		sh_chat_message(id, gHeroID, "Cambiaste de equipo y reivivste usando %s Poderes de Vudú, ahora morirás!", HeroName)
		user_kill(id, 1)

	}
}
//----------------------------------------------------------------------------------------------
public bomb_planted(id)
	BombPlanted = true

public sh_round_start()
	BombPlanted = false

public sh_round_end()
{
	BombPlanted = false
	
	// Reset the cooldown on round end, to start fresh for a new round
	for ( new id = 1; id <= SH_MAXSLOTS; id++ ) {
		if ( HasChucky[id] ) {
			gPlayerInCooldown[id] = false
		}
	}
}
	
// For remove residuals tasks
public client_disconnected(id) remove_task(id)
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang11274\\ f0\\ fs16 \n\\ par }
*/
