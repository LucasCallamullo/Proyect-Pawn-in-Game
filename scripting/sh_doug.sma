// FPS Doug!

/* CVARS - copy and paste to shconfig.cfg

//FPS Doug
doug_level 6
doug_knivespeed 400	//Speed of FPS Doug when holding knife
doug_extradamage 0	// 0 =  instand kill, any other value is added damage.
doug_blood_effects 1	// Blood sprite effects

*/

/*
* v1.3 - K-OS - Default release
*      - Made with some peaking into the source of sh_greenarrow,
*        miscstats, amx_gore_ultimate and wolverine
*
* v1.4 - Fr33m@n - 4/5/09
*      - Updated to be SH 1.2.0 compliant, removed amx compatibility (runtime error fixed)
*      - Cleaned up code
*      - Fixed minor mistake who made the 8th sound  never played ingame
*
* v1.5 - Fr33m@n - 4/11/09
*      - Made sounds constant
*      - Minor changes
*
* v1.6 - Fr33m@n - 5/20/09
*      - Replace user_kill stuff by sh_extra_damage
*      - Remove useless stuff, minor changes
*
* v1.7 - Fr33m@n - 6/6/09
*      - Add some blood effects (cvar)
*      - Minor fix about friendlyfire
*        Thanks to JTP10181 for blood effects (Ultimate Gore)
*        http://forums.alliedmods.net/showthread.php?p=19346
*
* v1.8 - Fr33m@n - 11/13/09
*      - Use of SH_DMG_KILL param instead of use damage = get_user_health(victim)
*      - Use of sh_get_weapon_slot instead of wpncheck
*/

//---------- User Changeable Defines --------//

// Comment out to force not removing handguns' weaponmodel
// Note: If you change anything here from default setting you must recompile the plugin
#define REMOVE_WPN_MODEL

//------- Do not edit below this point ------//

#include <superheromod>

// GLOBAL VARIABLES
new gHeroID
new const gHeroName[] = "Doug Headshot!"
new bool:gHasDoug[SH_MAXSLOTS+1]
new gSpriteBoom, gSpriteBloodDrop, gSpriteBloodSpray
new gPcvarExtraDmg, gPcvarBloodEffects

new const gBoomSound[8][] = {
	"shmod/fpsdoug/boomhs3x.wav",
	"shmod/fpsdoug/boomhs.wav",
	"shmod/fpsdoug/boomhsslow.wav",
	"shmod/fpsdoug/boomhsyeah.wav",
	"shmod/fpsdoug/danceallday.wav",
	"shmod/fpsdoug/hohohoyeah.wav",
	"shmod/fpsdoug/takethatbitch.wav",
	"shmod/fpsdoug/yeahehehe.wav"
}
//------------------------------------------------------------------------------------------------
//				Plugin Init and Precache					//
//------------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO FPS Doug", "1.8", "K-OS / Fr33m@n")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel 		= register_cvar("doug_level", "0")
	gPcvarExtraDmg 		= register_cvar("doug_extradamage", "4.0")
	gPcvarBloodEffects 	= register_cvar("doug_blood_effects", "1")

	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(gHeroName, pcvarLevel)
	sh_set_hero_info(gHeroID, "BOOM HEADSHOT!", "Mata de un tiro en la cabeza con DK.")

	// EVENTS
	register_event("CurWeapon", "weapon_change","be","1=1")
	
	// esto es oara que use solo una bala
	// register_forward(FM_CmdStart, "fwdPlayerCmdStart", 1)	//def 1
}

public plugin_precache()
{
	gSpriteBloodDrop = precache_model("sprites/blood.spr")
	gSpriteBloodSpray = precache_model("sprites/bloodspray.spr")
	gSpriteBoom = precache_model("sprites/fexplo1.spr")

	for ( new x = 0; x < 8; x++ ) {
		precache_sound(gBoomSound[x])
	}
}
//----------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode)
{
	if ( gHeroID != heroID ) return

	switch(mode) {
		case SH_HERO_ADD: {
			gHasDoug[id] = true
#if defined REMOVE_WPN_MODEL
			switch_model(id)
#endif
		}
		case SH_HERO_DROP: {
			gHasDoug[id] = false
		}
	}

	sh_debug_message(id, 1, "%s %s", gHeroName, mode ? "ADDED" : "DROPPED")
}

public sh_client_spawn(id)
{
	if ( gHasDoug[id] ) {
		ham_strip_weapon(id, "weapon_usp")
		ham_strip_weapon(id, "weapon_glock")
	}
}

/* public fwdPlayerCmdStart(id, iUc, iRandom)
{        
	// Agregado
	if ( !(id <= id <= SH_MAXSLOTS) || !is_user_alive(id) ) return
	if ( !gHasDoug[id] ) return
	
	cs_set_user_bpammo(id, CSW_DEAGLE, 3) 
	new iEntDeagle = find_ent_by_owner(-1, "weapon_deagle", id)

	if(is_user_alive(id) && get_user_weapon(id) == CSW_DEAGLE && cs_get_weapon_ammo(iEntDeagle) > 3) {
		cs_set_weapon_ammo(iEntDeagle, 3)
	}
}  */

public client_damage(attacker, victim, damage, wpnindex, hitplace)
{
	if ( !sh_is_active() ) return
	if ( !is_user_connected(victim) || !is_user_alive(attacker) ) return
	if ( cs_get_user_team(victim) == cs_get_user_team(attacker) ) return

	if ( gHasDoug[attacker] && sh_get_weapon_slot(wpnindex) == 2 && hitplace == 1) {
		static weaponName[32]
		get_weaponname(wpnindex, weaponName, charsmax(weaponName))
		replace(weaponName, charsmax(weaponName), "weapon_", "")

		new extraDamage = get_pcvar_num(gPcvarExtraDmg)
		if ( extraDamage > 0) {
			sh_extra_damage(victim, attacker, extraDamage, weaponName, 1)
			if ( is_user_alive(victim) ) return
			}
		else 	{
			sh_extra_damage(victim, attacker, damage, weaponName, 1, SH_DMG_KILL)
		}
		boom_effects(attacker, victim)
	}
}
//----------------------------------------------------------------------------------------------
#if defined REMOVE_WPN_MODEL
public weapon_change(id)
{
	if ( !sh_is_active() || !gHasDoug[id] ) return

	//weaponID = read_data(2)
	if ( sh_get_weapon_slot(read_data(2)) == 2 ) switch_model(id)
}
//----------------------------------------------------------------------------------------------
switch_model(id)
{
	if ( !sh_is_active() || !is_user_alive(id) || !gHasDoug[id] ) return

	// If user has a shield do not remove model, we don't want to see players with an invisible shield
	if ( cs_get_user_shield(id) ) return

	if ( sh_get_weapon_slot(get_user_weapon(id)) == 2 ) {
		set_pev(id, pev_weaponmodel2, "")
	}
}
#endif
//----------------------------------------------------------------------------------------------
boom_effects(attacker, victim)
{
	emit_sound(attacker, CHAN_AUTO, gBoomSound[random(8)], VOL_NORM, ATTN_NORM, 0, PITCH_NORM)

	new origin[3]
	get_user_origin(victim, origin)

	// Explosion
	message_begin(MSG_BROADCAST, SVC_TEMPENTITY)
	write_byte(TE_EXPLOSION)	// 3
	write_coord(origin[0])
	write_coord(origin[1])
	write_coord(origin[2] + 20)
	write_short(gSpriteBoom)	// sprite index
	write_byte(10)	// scale in 0.1's
	write_byte(15)	// framerate
	write_byte(0)	// flags
	message_end()

	if ( get_pcvar_num(gPcvarBloodEffects) ) {
		// Falling blood spray
		message_begin(MSG_BROADCAST, SVC_TEMPENTITY)
		write_byte(TE_BLOODSPRITE)	// 115
		write_coord(origin[0])
		write_coord(origin[1])
		write_coord(origin[2] + 40)
		write_short(gSpriteBloodSpray)	// sprite1 index
		write_short(gSpriteBloodDrop)		// sprite2 index
		write_byte(247)	// color
		write_byte(15)	// scale in 0.1's
		message_end()

		// Blood particule spray
		message_begin(MSG_BROADCAST, SVC_TEMPENTITY)
		write_byte(TE_BLOODSTREAM)	// 101
		write_coord(origin[0])
		write_coord(origin[1])
		write_coord(origin[2] + 40)
		engfunc(EngFunc_WriteCoord, random_float(-30.0, 30.0))	// spray vector
		engfunc(EngFunc_WriteCoord, random_float(-30.0, 30.0))
		engfunc(EngFunc_WriteCoord, random_float(80.0, 300.0))
		write_byte(70)	// color
		write_byte(random_num(100, 200))	//speed
		message_end()
	}
}
//----------------------------------------------------------------------------------------------
stock ham_strip_weapon(id,weapon[])
{
	if(!equal(weapon,"weapon_",7)) return 0;

	new wId = get_weaponid(weapon);
	if(!wId) return 0;
 
	new wEnt;
	while((wEnt = engfunc(EngFunc_FindEntityByString,wEnt,"classname",weapon)) && pev(wEnt,pev_owner) != id) {}
	if(!wEnt) return 0;

	if(get_user_weapon(id) == wId) ExecuteHamB(Ham_Weapon_RetireWeapon,wEnt);

	if(!ExecuteHamB(Ham_RemovePlayerItem,id,wEnt)) return 0;
	ExecuteHamB(Ham_Item_Kill,wEnt);

	set_pev(id,pev_weapons,pev(id,pev_weapons) & ~(1<<wId));

	return 1;
} 
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1030\\ f0\\ fs16 \n\\ par }
*/
