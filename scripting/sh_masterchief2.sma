// MASTER CHIEF! - Master Chief Petty Officer John-117 from the game Halo.

/* CVARS - copy and paste to shconfig.cfg

//Master Chief
masterchief_level 0
masterchief_health 100		//Default 100 (no extra health)
masterchief_armor 150		//Default 150
masterchief_p90mult 1.3		//Damage multiplyer for his P90

*/

/*
* v1.6 - vittu - 11/19/07
*      - Added use of const requires amxx 1.76d or higher
*      - Added define options for more customization.
*      - Team colored models can be set with define now.
*      - Now verifies models are loaded else does not use them.
*
* v1.5 - vittu - 6/3/06
*      - Updated to amxmodx only, requires amxx 1.75 or higher.
*      - Removed engine for fakemeta only support.
*      - Changed 2 default cvar values.
*      - Added excessive checking plus other minor code changes.
*      - Fixed rare issue with stale varible giving users the hero that shouldn't have it.
*
* v1.4 - vittu - 6/14/05
*      - Minor code clean up.
*      - Lowered glow value so it's not so bright.
*
* v1.3 - vittu - 1/3/05
*      - Fixed up task set for team glow, so as not to interfere
*         with other tasks when it's removed.
*      - This should be the final version for now.
*
* v1.2 - vittu - 12/26/04
*      - Changed 2 default cvars, gravity and speed to avoid bugs.
*      - Changed give weapon time on ResetHUD, to avoid bugs.
*
* v1.1 - vittu - 12/21/04
*      - set a delay to change player model, better for servers with
*         sh_loadimmediate 0.
*
* v1.0 - vittu - 12/20/04
*      - Complete rework of entire code, started from scratch.
*      - Changed player model to automatic rather than a keydown.
*      - Added p_ weapon model (for 3rd person view).
*
*   Concept by Spartan-117 and JASerrano
*   Player and Weapon models by Strykerwolf (aka Alex Wright) & Grenappels
*/

//---------- User Changeable Defines --------//

//------- Do not edit below this point ------// 
#include <superheromod>

// GLOBAL VARIABLES
new gHeroID
new HeroName[] = "Master Chief"
new bool:HasMasterChief[SH_MAXSLOTS+1]
new CvarP90DmgMult

new const Model_Player_T[] = "models/player/masterchief_t/masterchief_t.mdl"
new const Model_Player_CT[] = "models/player/masterchief_ct/masterchief_ct.mdl"
new const Model_Player_Name[2][] = {
	"masterchief_t",
	"masterchief_ct"
}

new const MasterChiefSound[] = "items/suitchargeno1.wav"

new const Model_V_P90[] = "models/shmod/masterchief_v_p90.mdl"
new const Model_P_P90[] = "models/shmod/masterchief_p_p90.mdl"

//----------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Master Chief", "1.6", "Spartan-117/vittu")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel	= register_cvar("masterchief_level", "0")
	new pcvarHealth	= register_cvar("masterchief_health", "100")
	new pcvarArmor	= register_cvar("masterchief_armor", "150")
	CvarP90DmgMult 	= register_cvar("masterchief_p90mult", "1.3")

	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(HeroName, pcvarLevel);
	sh_set_hero_info(gHeroID, "Proyecto SPARTAN.", "Conviertete en Master Chief - Obtén un MJOLNIR Traje de Batalla y MA5B Assault Rifle (P90), el cual hace más daño.");
	sh_set_hero_hpap(gHeroID, pcvarHealth, pcvarArmor)
	
	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)
	// EVENTS
	register_event("Damage", "masterchief_damage", "b", "2!0")

	RegisterHam(Ham_Item_Deploy, "weapon_p90", "P90_Deploy", 1)
}

public plugin_precache()
{
	precache_sound(MasterChiefSound)

	if ( file_exists(Model_Player_T) )
		precache_model(Model_Player_T)
	else 
		log_amx("[SH](%s)Aborted loading ^"%s^", file does not exist on server", HeroName, Model_Player_T)
	
	if ( file_exists(Model_Player_CT) )
		precache_model(Model_Player_CT)
	else 
		log_amx("[SH](%s)Aborted loading ^"%s^", file does not exist on server", HeroName, Model_Player_CT)

	if ( file_exists(Model_V_P90) )
		precache_model(Model_V_P90)
	else
		log_amx("[SH](%s)Aborted loading ^"%s^", file does not exist on server", HeroName, Model_V_P90)

	if ( file_exists(Model_P_P90) ) 
		precache_model(Model_P_P90)
	else 
		log_amx("[SH](%s)Aborted loading ^"%s^", file does not exist on server", HeroName, Model_P_P90)
}
//------------------------------------------------------------------------------------------------
//				Hero INIT and KEY						//
//------------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode) 
{ 
	if ( heroID != gHeroID ) return
    
	switch(mode) {
		case SH_HERO_ADD: {
			HasMasterChief[id] = true
			masterchief_weapons(id)		
			switch_model(id)
			masterchief_tasks(id)
		}
		case SH_HERO_DROP: {
			HasMasterChief[id] = false
			masterchief_morph_unmoprh(id)
		}
	}
	
	sh_debug_message(id, 1, "%s %s", HeroName, mode ? "ADDED" : "DROPPED")
}

public sh_client_spawn(id)
{
	if ( HasMasterChief[id] ) {
		set_task(0.1, "masterchief_weapons", id)
		masterchief_tasks(id)
	}
}

public masterchief_weapons(id)
{
	if ( !is_user_alive(id) ) return
	sh_give_weapon(id, CSW_P90)
	sh_give_item(id,"ammo_57mm")
	sh_give_item(id,"ammo_57mm")
}

switch_model(id)
{
	if ( !is_user_alive(id) ) return

	new clip, ammo, wpnid = get_user_weapon(id, clip, ammo)

	if ( wpnid == CSW_P90 ) {
		set_pev(id, pev_viewmodel2, Model_V_P90)
		set_pev(id, pev_weaponmodel2, Model_P_P90)
	}
}

public P90_Deploy(iEnt)
{
	new id = get_pdata_cbase(iEnt, 41, 4)	// 41 y 4 son constantes van siempre
	if ( !is_user_alive(id) || !HasMasterChief[id] ) return HAM_IGNORED; 
	set_pev(id, pev_viewmodel2, Model_V_P90)
	set_pev(id, pev_weaponmodel2, Model_P_P90)
	
	if ( read_data(3) == 0 )
		shReloadAmmo(id, 0)
	
	return HAM_IGNORED; 
}
//----------------------------------------------------------------------------------------------
//				DAMAGE EVENT
//----------------------------------------------------------------------------------------------
public masterchief_damage(id)
{
	if ( !is_user_alive(id) ) return

	new weapon, bodypart, attacker = get_user_attacker(id, weapon, bodypart)

	if ( attacker <= 0 || attacker > SH_MAXSLOTS ) return

	if ( HasMasterChief[attacker] && weapon == CSW_P90 ) {
		new damage = read_data(2)
		new headshot = bodypart == 1 ? 1 : 0

		// do extra damage
		new extraDamage = floatround(damage * get_pcvar_float(CvarP90DmgMult) - damage)
		if ( extraDamage > 0 )
			shExtraDamage(id, attacker, extraDamage, "p90", headshot)
	}
}
//----------------------------------------------------------------------------------------------
//				SET MODEL SKINS PLAYER
//----------------------------------------------------------------------------------------------
masterchief_tasks(id) set_task(1.0, "masterchief_morph_unmoprh", id)

public masterchief_morph_unmoprh(id)
{
	if ( !is_user_alive(id) ) return
	
	if ( HasMasterChief[id] ) { 
		// Done this way for safety (ie user is spec alive)
		switch(cs_get_user_team(id)) {
			case CS_TEAM_T: cs_set_user_model(id, Model_Player_Name[0])
			case CS_TEAM_CT: cs_set_user_model(id, Model_Player_Name[1])
			default: return
		}
	}
	else cs_reset_user_model(id)
	
	masterchief_sound(id)
}

masterchief_sound(id) {
	emit_sound(id, CHAN_AUTO, MasterChiefSound, 0.2, ATTN_NORM, SND_STOP, PITCH_NORM)
	emit_sound(id, CHAN_AUTO, MasterChiefSound, 0.2, ATTN_NORM, 0, PITCH_NORM)
}

public client_connect(id)
	HasMasterChief[id] = false
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang11274\\ f0\\ fs16 \n\\ par }
*/
