// PHOENIX! - The fiery bird from Mythology. Rebirth from the ashes of it's burning death.

/* CVARS - copy and paste to shconfig.cfg

//Phoenix
phoenix_level 8
phoenix_cooldown 120	//Amount of time before next available respawn (Default 120)
phoenix_radius 375	//Radius of people affected by blast (Default 375)
phoenix_maxdamage 90	//Maximum damage dealt spread over radius (Default 90)

*/

/*
* v1.3 - Fr33m@n - 5/20/10
*      - Added an unstuck function in case users are stuck in an ennemy.
*
* v1.2 - Fr33m@n - 5/3/10
*      - Updated to be SH 1.2.0 compliant, removed amx compatibility.
*      - Code cleaned up.
*      - Users now look at the point where they aimed before.
*      - Crouch stuck kill removed.
*
* v1.1 - vittu - 12/31/05
*      - Code cleaned up.
*      - Fixed respawn issues.
*      - Changed damage to be an actual radius damage.
*      - Added extra sound.
*/

#include <superheromod>

// GLOBAL VARIABLES
new gHeroID
new const gHeroName[] = "Phoenix"
new bool:gHasPhoenix[SH_MAXSLOTS+1]

// for  un stuck
new Float:gFVecOrigin[SH_MAXSLOTS+1][3]
new Float:gFVecAngles[SH_MAXSLOTS+1][3]

new const Float:VEC_DUCK_HULL_MIN[3]	= {-16.0, -16.0, -18.0 }
new const Float:VEC_DUCK_HULL_MAX[3]	= { 16.0,  16.0,  32.0 }
new const Float:VEC_DUCK_VIEW[3]	= {  0.0,   0.0,  12.0 }
new const Float:VEC_NULL[3]		= {  0.0,   0.0,   0.0 }

new const gSoundReBirth[] = "ambience/port_suckin1.wav"
new const gSoundEagle[] = "ambience/3dmeagle.wav"

new gSpriteSmoke, gSpriteRing, gSpriteExplosion, gMsgSync
new gPcvarRadius, gPcvarMaxDamage
//----------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Phoenix", "1.3", "[FTW]-S.W.A.T / vittu / Fr33m@n")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel 	= register_cvar("phoenix_level", "8")
	gPcvarRadius 	= register_cvar("phoenix_radius", "375")
	gPcvarMaxDamage	= register_cvar("phoenix_maxdamage", "90")

	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(gHeroName, pcvarLevel)
	sh_set_hero_info(gHeroID, "Re-Nacimiento Fénix.", "Como un Fénix te levantarás de las cenizas, y volverás a la vida.")

	gMsgSync = CreateHudSyncObj()
}
//----------------------------------------------------------------------------------------------
public plugin_precache()
{
	gSpriteSmoke = precache_model("sprites/steam1.spr")
	gSpriteRing = precache_model("sprites/white.spr")
	gSpriteExplosion = precache_model("sprites/explode1.spr")
	precache_sound(gSoundReBirth)
	precache_sound(gSoundEagle)
}
//----------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode)
{
	if ( gHeroID != heroID ) return

	gHasPhoenix[id] = mode ? true : false

	sh_debug_message(id, 1, "%s %s", gHeroName, mode ? "ADDED" : "DROPPED")
}
//----------------------------------------------------------------------------------------------
public sh_client_death(victim, attacker)
{
	if ( !sh_is_active() || !sh_is_inround() ) return
	if ( !is_user_connected(victim) || victim == attacker ) return
	if ( !gHasPhoenix[victim] || gPlayerInCooldown[victim] ) return

	// Save users origin on death
	pev(victim, pev_origin, gFVecOrigin[victim])
	pev(victim, pev_v_angle, gFVecAngles[victim])

	// Respawn him faster then Zues, let this power be used before Zues's
	// never set higher then 1.9 or lower then 0.5
	/* el task esta puesto en 0.x porque segun el mas chico se activa primero ese heroe.
	mangekyou = 0,5
	chucky = 0.6 
	phoenix = 0.7
	shaman = 0.8
	dr.strange = 0.9
	majin buu = 1.0
	grandmaster = 1.1 // pero esta se superpone porque es la primera en usarse 
	uchiha revenge = 1.2
	torneo = 1.5 */
	set_task(0.7, "phoenix_respawn", victim)
}
//----------------------------------------------------------------------------------------------
public phoenix_respawn(id)
{
	if ( is_user_alive(id) || gPlayerInCooldown[id] ) return

	emit_sound(id, CHAN_STATIC, gSoundReBirth, 0.2, ATTN_NORM, 0, PITCH_NORM)

	sh_chat_message(id, gHeroID, "Usaste el Poder del Ave %s para Volver de las Cenizas!", gHeroName)

	// Double spawn prevents the no HUD glitch
	// This should eventually be changed to use a better method
	spawn(id)
	spawn(id)  
	//ExecuteHamB(Ham_CS_RoundRespawn, id)		// Use this no for bots 

	// Respawned by Phoenix, it's ok to set cooldown now.
	gPlayerInCooldown[id] = true

	phoenix_teleport(id)
	blow_up(id)

	// If player is stuck, try to unstuck him
	new hulltype = (pev(id, pev_flags) & FL_DUCKING) ? HULL_HEAD : HULL_HUMAN
	if ( !sh_hull_vacant(id, gFVecOrigin[id], hulltype) ) {
		unstuck(id, hulltype)
	}
}
//----------------------------------------------------------------------------------------------
phoenix_teleport(id)
{
	emit_sound(id, CHAN_STATIC, gSoundEagle, 0.6, ATTN_NORM, 0, PITCH_NORM)

	sh_set_rendering(id, 248, 20, 25, 16, kRenderFxGlowShell)
	set_task(3.0, "phoenix_unglow", id)

	// Thanks to Connor for duck and angles part
	if ( is_user_alive(id) && gFVecOrigin[id][0] ) {
		set_pev(id, pev_flags, pev(id, pev_flags) | FL_DUCKING)
		engfunc(EngFunc_SetSize, id, VEC_DUCK_HULL_MIN, VEC_DUCK_HULL_MAX)
		engfunc(EngFunc_SetOrigin, id, gFVecOrigin[id])
		set_pev(id, pev_view_ofs, VEC_DUCK_VIEW)

		set_pev(id, pev_angles, gFVecAngles[id])
		set_pev(id, pev_v_angle, VEC_NULL)
		set_pev(id, pev_fixangle, 1)
	}

	// Teleport Effects
	message_begin(MSG_BROADCAST, SVC_TEMPENTITY)
	write_byte(TE_TELEPORT)						// 11
	engfunc(EngFunc_WriteCoord, gFVecOrigin[id][0])		// start position
	engfunc(EngFunc_WriteCoord, gFVecOrigin[id][1])
	engfunc(EngFunc_WriteCoord, gFVecOrigin[id][2])
	message_end()
}
//----------------------------------------------------------------------------------------------
public phoenix_unglow(id)
	sh_set_rendering(id)
//----------------------------------------------------------------------------------------------
blow_up(id)
{
	new Float:dRatio, Float:distanceBetween, damage
	new Float:playerOrigin[3], name[32]
	new Float:dmgRadius = get_pcvar_float(gPcvarRadius)
	new maxDamage = get_pcvar_num(gPcvarMaxDamage)
	new FFOn = sh_friendlyfire_on()
	new CsTeams:idTeam = cs_get_user_team(id)

	// blowup even if dead
	explode_effect(gFVecOrigin[id], dmgRadius)

	get_user_name(id, name, charsmax(name))
	set_hudmessage(248, 20, 25, 0.05, 0.65, 2, 0.02, 3.0, 0.01, 0.1, -1)
	ShowSyncHudMsg(id, gMsgSync, "%s Renaciste usando el poder del %s!", name, gHeroName)

	new players[SH_MAXSLOTS], playerCount, player
	get_players(players, playerCount, "ah")

	for ( new i = 0; i < playerCount; i++ ) {
		player = players[i]

		if ( player == id ) continue

		pev(player, pev_origin, playerOrigin)
		distanceBetween = vector_distance(gFVecOrigin[id], playerOrigin)

		if ( distanceBetween <= dmgRadius ) {
			if ( FFOn || idTeam != cs_get_user_team(player) ) {
				dRatio = distanceBetween / dmgRadius
				damage = maxDamage - floatround(maxDamage * dRatio)
				if ( !damage ) damage = 1	// Incase damage cvar is really low cause something if within the radius 
				sh_extra_damage(player, id, damage, "Phoenix Re-Birth", _, SH_DMG_NORM, true)
			}
		}
	}
}
//----------------------------------------------------------------------------------------------
explode_effect(Float:vec1[3], Float:dmgRadius)
{
	// Ring
	engfunc(EngFunc_MessageBegin, MSG_BROADCAST, SVC_TEMPENTITY, vec1, 0)
	write_byte(TE_BEAMCYLINDER)			// 21
	engfunc(EngFunc_WriteCoord, vec1[0])	// center position
	engfunc(EngFunc_WriteCoord, vec1[1])
	engfunc(EngFunc_WriteCoord, vec1[2] + 10)
	engfunc(EngFunc_WriteCoord, vec1[0])	// axis and radius
	engfunc(EngFunc_WriteCoord, vec1[1])
	engfunc(EngFunc_WriteCoord, vec1[2] + (dmgRadius * 3.5))
	write_short(gSpriteRing)	// sprite index
	write_byte(0)			// starting frame
	write_byte(0)			// frame rate in 0.1's
	write_byte(2)			// life in 0.1's
	write_byte(20)			// line width in 0.1's
	write_byte(0)			// noise amplitude in 0.01's
	write_byte(248)			// red
	write_byte(20)			// green
	write_byte(25)			// blue
	write_byte(255)			// brightness
	write_byte(0)			// scroll speed in 0.1's
	message_end()

	// Explosion2
	message_begin(MSG_BROADCAST, SVC_TEMPENTITY)
	write_byte(TE_EXPLOSION2)			// 12
	engfunc(EngFunc_WriteCoord, vec1[0])	// start position
	engfunc(EngFunc_WriteCoord, vec1[1])
	engfunc(EngFunc_WriteCoord, vec1[2])
	write_byte(188)	// starting color
	write_byte(10)	// num colors
	message_end()

	// Explosion
	engfunc(EngFunc_MessageBegin, MSG_BROADCAST, SVC_TEMPENTITY, vec1, 0)
	write_byte(TE_EXPLOSION)			// 3
	engfunc(EngFunc_WriteCoord, vec1[0])	// start position 
	engfunc(EngFunc_WriteCoord, vec1[1])
	engfunc(EngFunc_WriteCoord, vec1[2])
	write_short(gSpriteExplosion)			// sprite index
	write_byte(floatround(dmgRadius / 9))	// scale in 0.1's 
	write_byte(10)					// framerate
	write_byte(TE_EXPLFLAG_NONE)			// flags
	message_end()

	// Smoke
	engfunc(EngFunc_MessageBegin, MSG_BROADCAST, SVC_TEMPENTITY, vec1, 0)
	write_byte(TE_SMOKE)				// 5
	engfunc(EngFunc_WriteCoord, vec1[0])	// start position
	engfunc(EngFunc_WriteCoord, vec1[1])
	engfunc(EngFunc_WriteCoord, vec1[2])
	write_short(gSpriteSmoke)			// sprite index
	write_byte(floatround(dmgRadius / 14))	// scale in 0.1's
	write_byte(10)					// framerate
	message_end()
}
//----------------------------------------------------------------------------------------------
//Thank you from AMXX NS unstuck plugin
unstuck(id, hulltype)
{
	new Float:new_origin[3], distance, i
	distance = 32

	while ( distance < 1000 ) {	// 1000 is just incase, should never get anywhere near that
		for ( i = 0; i < 128; i++ ) {
			new_origin[0] = random_float(gFVecOrigin[id][0] - distance, gFVecOrigin[id][0] + distance)
			new_origin[1] = random_float(gFVecOrigin[id][1] - distance, gFVecOrigin[id][1] + distance)
			new_origin[2] = random_float(gFVecOrigin[id][2] - distance, gFVecOrigin[id][2] + distance)

			if ( fm_trace_hull(new_origin, hulltype, id) == 0 ) {
				engfunc(EngFunc_SetOrigin, id, new_origin)
				return
			}
		}
		distance += 32
	}
}
//----------------------------------------------------------------------------------------------
//Stock from fakemeta_util.inc
stock fm_trace_hull(const Float:origin[3], hull, ignoredent = 0, ignoremonsters = 0) {
	new result = 0;
	engfunc(EngFunc_TraceHull, origin, origin, ignoremonsters, hull, ignoredent > 0 ? ignoredent : 0, 0);

	if (get_tr2(0, TR_StartSolid))
		result += 1;
	if (get_tr2(0, TR_AllSolid))
		result += 2;
	if (!get_tr2(0, TR_InOpen))
		result += 4;

	return result;
}
//----------------------------------------------------------------------------------------------
public sh_round_end()
{
	new players[SH_MAXSLOTS], numplayers, id 
	get_players(players, numplayers, "h")

	for ( new i = 0; i < numplayers; i++ ) {
		
		id = players[i]
		
		gPlayerInCooldown[id ] = false
		remove_task(id)
	}
}
//----------------------------------------------------------------------------------------------
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1030\\ f0\\ fs16 \n\\ par }
*/
