//YODA! - from Star Wars. Need I say more.
/*
//Yoda
yoda_level 9
yoda_cooldown 10	//Time in seconds until yoda can push again
yoda_radius 400	//How close does enemy have to be in order to push them (def=400)
yoda_power 600		//Force of the push, velocity multiplier (def=600)
yoda_damage 10 	//Amount of damage a push does to an enemy (def=10)
yoda_selfdmg 0		//Amount of damage using push does to self (def=0)

*/
/*
* v1.2 - vittu - 6/23/05
*      - Minor code clean up.
*
* v1.1 - vittu - 4/16/05
*      - Cleaned up code.
*      - Fixed self damage cvar to work and made it define amount of damage instead.
*      - Changed cooldown to only be set when an enemy is actually pushed.
*      - Added sound to damage caused.
*      - Added a stun so enemies can't easily push against the push force.
*
*   from original code "MORE JEDI POWERS TO BE ADDED :)"
*/
// 1 = send another plugins information about cooldown, 0 = don't send
#define SEND_COOLDOWN 1

#include <superheromod>
#include <Vexd_Utilities>

// GLOBAL VARIABLES
new gHeroID
new gHeroName[]="Yoda"
new bool:gHasYodaPower[SH_MAXSLOTS+1]
new gPcvarCooldown, gPcvarRadius, gPcvarPower, gPcvarDamage, gPcvarSelfdmg

#if SEND_COOLDOWN
	new Float:YodaUsedTime[SH_MAXSLOTS+1]
#endif
//------------------------------------------------------------------------------------------------
//				Plugin Init and Precache					//
//------------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Yoda", "1.2", "AssKicR / Freecode")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	new pcvarLevel 	= register_cvar("yoda_level", "9")
	gPcvarCooldown 	= register_cvar("yoda_cooldown", "15")
	gPcvarRadius	= register_cvar("yoda_radius", "410")
	gPcvarPower	= register_cvar("yoda_power", "600")
	gPcvarDamage	= register_cvar("yoda_damage", "20")
	gPcvarSelfdmg	= register_cvar("yoda_selfdmg", "0")

	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	gHeroID = sh_create_hero(gHeroName, pcvarLevel);
	sh_set_hero_info(gHeroID, "Force Push!", "Empuja a tus enemigos con el poder de la Fuerza del Poder Jedi.");
	sh_set_hero_bind(gHeroID); 

	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)
	// EVENTS
	register_event("CurWeapon", "weaponChange","be","1=1")
}

public plugin_precache()
{
	precache_sound("shmod/yoda_forcepush.wav")
	precache_sound("player/pl_pain2.wav")
	precache_model("models/shmod/yoda_v.mdl")
	precache_model("models/shmod/yoda_p.mdl")
}
//------------------------------------------------------------------------------------------------
//				Hero INIT and KEY						//
//------------------------------------------------------------------------------------------------
public sh_hero_init(id, heroID, mode) 
{ 
	if ( heroID != gHeroID ) return
    
	switch(mode) {
		case SH_HERO_ADD: {
			gHasYodaPower[id] = true
			switchmodel(id)
			gPlayerInCooldown[id] = false
		}
		case SH_HERO_DROP: {
			gHasYodaPower[id] = false;
		}
	}
}

public sh_hero_key(id, heroID, key) 
{ 
	if ( heroID != gHeroID || !sh_is_inround() ) return PLUGIN_HANDLED;
	if ( !is_user_alive(id) || !gHasYodaPower[id] ) return PLUGIN_HANDLED;
    
	if ( key == SH_KEYDOWN ) {
		
		if ( gPlayerUltimateUsed[id] ) {
			playSoundDenySelect(id)
			return PLUGIN_HANDLED
		}
		
		force_push(id)
	}
	
	return PLUGIN_HANDLED
}
#if SEND_COOLDOWN
public sendYodaCooldown(id)
{
	new cooldown
	if (gPlayerInCooldown[id])
		cooldown = floatround( get_pcvar_num(gPcvarCooldown) - get_gametime() + YodaUsedTime[id] + 0.4 )
	else
		cooldown = -1
	return cooldown
}
#endif
//------------------------------------------------------------------------------------------------
//				Evento Force Push (Power)					//
//------------------------------------------------------------------------------------------------
public force_push(id)
{
	if ( !is_user_alive(id) ) return

	new team[33], players[SH_MAXSLOTS], pnum
	new origin[3], vorigin[3], parm[4], distance
	new Float:tempVelocity[3] = {0.0, 0.0, 200.0}
	new bool:enemyPushed = false
	get_user_team(id, team, 32)

	// Find all alive enemies
	if ( equali(team, "CT") ) {
		get_players(players, pnum, "ae", "TERRORIST")
		}
	else 	{
		get_players(players, pnum, "ae", "CT")
	}

	get_user_origin(id, origin)
	for ( new vic = 0; vic < pnum; vic++ ) {
		if( !is_user_alive(players[vic]) ) continue
		get_user_origin(players[vic], vorigin)
		distance = get_distance(origin, vorigin)
		if ( distance < get_pcvar_num(gPcvarRadius) ) {

			// Set cooldown/sound/self damage only once, if push is used
			if ( !enemyPushed ) {
				
				new Float:seconds = get_pcvar_float(gPcvarCooldown)
				if ( seconds > 0.0 ) {
					sh_set_cooldown(id, seconds)
					#if SEND_COOLDOWN
						YodaUsedTime[id] = get_gametime()
					#endif
				}

				emit_sound(id, CHAN_ITEM, "shmod/yoda_forcepush.wav", 1.0, ATTN_NORM, 0, PITCH_NORM)

				// Do damage to Yoda?
				if ( get_cvar_num("yoda_selfdmg") > 0 ) {
					shExtraDamage(id, id, get_pcvar_num(gPcvarSelfdmg), "Force Push")
				}
				enemyPushed = true
			}

			parm[0] = ((vorigin[0] - origin[0]) / distance) * get_pcvar_num(gPcvarPower)
			parm[1] = ((vorigin[1] - origin[1]) / distance) * get_pcvar_num(gPcvarPower)
			parm[2] = players[vic]
			parm[3] = id

			// Stun enemy makes them easier to push
			sh_set_stun(players[vic], 1.0, 10.0)

			// First lift them
			Entvars_Set_Vector(players[vic], EV_VEC_velocity, tempVelocity)

			// Then push them back in x seconds after lift and do some damage
			set_task(0.1, "move_enemy", 0, parm, 4)
		}
	}

	if ( !enemyPushed && is_user_alive(id) ) {
		sh_chat_message(id, gHeroID, "No hay enemigos dentro del alcance!")
		playSoundDenySelect(id)
	}
}

public move_enemy(parm[])
{
	new victim = parm[2]
	new id = parm[3]

	new Float:fl_velocity[3]
	fl_velocity[0] = float(parm[0])
	fl_velocity[1] = float(parm[1])
	fl_velocity[2] = 200.0

	Entvars_Set_Vector(victim, EV_VEC_velocity, fl_velocity)

	// do some damage
	if ( get_cvar_num("yoda_damage") > 0 ) {
		emit_sound(victim, CHAN_BODY, "player/pl_pain2.wav", 1.0, ATTN_NORM, 0, PITCH_NORM)

		if ( !is_user_alive(victim) ) return

		shExtraDamage(victim, id, get_pcvar_num(gPcvarDamage), "Force Push")
	}
}
//------------------------------------------------------------------------------------------------
//				Spawn and ChangeModels Faka					//
//------------------------------------------------------------------------------------------------
public sh_client_spawn(id)
	gPlayerUltimateUsed[id] = false

public weaponChange(id)
{
	if ( !is_user_alive(id) || !gHasYodaPower[id] || !shModActive() ) return

	new wpnid = read_data(2)		//new clip, ammo, wpnid = get_user_weapon(id,clip,ammo)
	if ( wpnid == CSW_KNIFE ) switchmodel(id)
}

public switchmodel(id)
{
	//If user is holding a shield do not change model, since we don't have one with a shield
	new v_mdl[32]
	Entvars_Get_String(id, EV_SZ_viewmodel, v_mdl, 31)
	if ( containi(v_mdl, "v_shield_") != -1 ) return
	
	new wpnid = read_data(2)
	if (wpnid == CSW_KNIFE) {
		Entvars_Set_String(id, EV_SZ_viewmodel, "models/shmod/yoda_v.mdl")
		Entvars_Set_String(id, EV_SZ_weaponmodel, "models/shmod/yoda_p.mdl")
	}
}
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang11274\\ f0\\ fs16 \n\\ par }
*/
